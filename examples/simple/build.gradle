/*
 * Copyright (C) 2016 Adri치n Medra침o Calvo
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License as
 * published by the Free Software Foundation; either version 2 of the
 * License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful, but
 * WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
 * 02111-1307, USA.
 *
 * Written by:
 *     Adri치n Medra침o Calvo <adrian@medranocalvo.com>
 */

buildscript {
    /*
     * Change these to fit your environment.  They can be provided to gradlew's
     * command line via the -P parameter, e.g. -PkawaJarPath="/abc/kawa.jar".
     */
    project.ext {
        if (!project.hasProperty('androidBuildToolsVersion')) {
            project.ext.androidBuildToolsVersion = '23.0.1'
        }
        if (!project.hasProperty('androidCompileSdkVersion')) {
            project.ext.androidCompileSdkVersion = 17
        }
        if (!project.hasProperty('kawaJarPath')) {
            project.ext.kawaJarPath = '/usr/share/java/kawa.jar'
        }
        if (!project.hasProperty('androidGradlePluginVersion')) {
            project.ext.androidGradlePluginVersion = '1.5.0'
        }
    }

    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        /* Add Android build system. */
        classpath "com.android.tools.build:gradle:${androidGradlePluginVersion}"
    }
}

plugins {
    /*
     * This plugin is used to override project properties via the command line.
     * This allows us to change various properties when testing, most
     * importantly plugin versions and the build directory location
     * (project.buildDir).
     */
    id 'nebula.override' version '3.0.2'
    /*
     * Need to use the new plugins script block to get automatic injection of
     * code under test.  See:
     * https://docs.gradle.org/current/userguide/test_kit.html#sub:test-kit-classpath-injection
     */
    id "com.medranocalvo.gradle.gradle-android-kawa-plugin" version "0.9"
}

/* Apply the Kawa Android plugin. */
apply plugin: 'com.android.application'
apply plugin: "com.medranocalvo.gradle.gradle-android-kawa-plugin"

dependencies {
    /* Add path to kawa.jar. */
    compile files(kawaJarPath)
}

/* General Kawa configuration. */
kawa {
    language 'scheme'
}

android {
    buildToolsVersion androidBuildToolsVersion
    compileSdkVersion androidCompileSdkVersion
    sourceSets {
        main {
            kawa {
                /*
                 * Paths where Scheme sources are looked up can be specifyed
                 * for each source set.  The default path for the 'main' source
                 * set is "src/main/scm".
                 */
                srcDir "src/some/other/path"
            }
        }
    }

    /*
     * Kawa configuration for android.  Enable inlining (it is enabled by
     * default anyway).
     */
    kawa {
        inline true
    }

    buildTypes {
        debug {
            /*
             * BuildType-specific Kawa configuration.  Disable inlining for
             * debug builds, overriding the android configuration.
             */
            kawa {
                inline false
            }
        }
    }
}

